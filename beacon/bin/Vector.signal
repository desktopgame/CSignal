import "Array"
import "Exception"
import "VectorIterator"

namespace beacon.lang {
	class Vector<|T|> : Iterable<|T|> {
		private:
		Array<|T|> proxy;
		Int length;
		Int capacity;

		public:
		ctor new() {
			this.length = 0;
			this.capacity = 16;
			this.proxy = new Array<|T|>(this.capacity);
		}

		def push(T o) -> Void {
			if(this.length < this.capacity) {
				this.proxy.set(this.length, o);
			} elif (this.length >= this.capacity) {
				var newLen = this.length + (this.length / 2);
				var newArr = new Array(newLen);
				Array.copy<|T|>(this.proxy, 0, newArr, 0, this.length);
				this.proxy = newArr;
				this.proxy.set(this.length, o);
			}
			this.length = this.length + 1;
		}

		def pop() -> T {
			if(this.length == 0) {
				throw new Exception("not added any objects");
			}
			var ret = this.proxy.get(this.length - 1);
			this.length = this.length - 1;
			return ret;
		}

		def set(Int index, T a) -> Void {
			this.checkRange(index);
			this.proxy.set(index, a);
		}

		def get(Int index) -> T {
			this.checkRange(index);
			return this.proxy.get(index);
		}

		def length() -> Int {
			return this.length;
		}

		def iterate() -> Iterator<|T|> {
			return new VectorIterator<|T|>(this);
		}

		private:
		def checkRange(Int index) -> Void {
			if(index < 0 || index >= this.length) {
				throw new Exception("Out Of Range");
			}
		}
	}
}